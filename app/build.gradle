apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))
//获取时间戳
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy_MMdd_HHmm')
    return formattedDate
}
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.shen.keep"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.1"
    }

    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }



    //修改生成的apk名字
    applicationVariants.all{
        variant->
            variant.outputs.each {
                output->
                    def oldFile = output.outputFile
                    if(variant.buildType.name.equals('release')){
                        def releaseApkName = 'keep_' + defaultConfig.versionName +
                                '_'  + getDate() +'.apk'
                        output.outputFile = new File(oldFile.parent, releaseApkName)
                    }
            }
    }

}

greendao{
    schemaVersion 1
    daoPackage 'com.shen.keep.app.db'
    targetGenDir 'src/main/java'
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'org.greenrobot:greendao-generator:3.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

}
